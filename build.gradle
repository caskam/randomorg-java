apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.mishadoff'
version = '0.1.0'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}
 
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
 
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
 
            pom.project {
               name 'randomorg'
               packaging 'jar'
               description 'Random generator via atmospheric noise random.org'
               url 'https://github.com/mishadoff/randomorg'
 
               scm {
                   url 'git@github.com:mishadoff/randomorg.git'
                   connection 'scm:git@github.com:mishadoff/randomorg.git'
                   developerConnection 'scm:git@github.com:mishadoff/randomorg.git'
               }
 
               licenses {
                   license {
                       name 'Eclipse Public License 1.0'
                       url 'https://www.eclipse.org/legal/epl-v10.html'
                       distribution 'repo'
                   }
               }
 
               developers {
                   developer {
                       id 'mishadoff'
                       name 'Mykhailo Kozik'
                   }
               }
           }
        }
    }
}